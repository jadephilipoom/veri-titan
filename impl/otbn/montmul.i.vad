include "../../arch/otbn/decls.i.vad"
include "mont_loop.i.vad"

ghost procedure montmul_inv_lemma_0(
       ghost a: seq(uint256),
       ghost x: seq(uint256), 
       ghost i: int,
       ghost y: seq(uint256), 
       ghost rsa: rsa_params) extern;

#verbatim
include "../../arch/otbn/decls.i.dfy"

include "mont_loop.i.dfy"
include "cond_sub_to_reg.i.dfy"

include "../../../impl/otbn/mont_loop_lemmas.i.dfy"
include "../../../impl/otbn/montmul_lemmas.i.dfy"
include "../../../impl/otbn/rsa_ops.i.dfy"

module montmul {

import opened bv_ops
import opened rsa_ops

import opened ot_machine
import opened ot_interp
import opened ot_decls
import opened ot_vale

import opened mont_loop
import opened mont_loop_lemmas
import opened montmul_lemmas

import opened BASE_256_Seq

#endverbatim

function SeqZero #[a: Type(0)](len: int): seq(a) extern;

ghost procedure LemmaSeqZero #[a: Type(0)](ghost xs: seq(a)) extern;

procedure montmul_0(ghost vars: mvars)
    returns (ghost a_view: seq(uint256))

    {:frame false}

    requires
        x9 == 3;
        x10 == 4;
        x11 == 2;
        x30 == NUM_WORDS;
    
        w31 == 0;

        mvars_inv(vars, heap, x20, x19, x16, x17, NA, NA);

    ensures
        x9 == old(x9);
        x11 == 2;
        x20 == old(x20);
        x21 == old(x21);
        x29 == old(x29);
        x30 == old(x30);
        x31 == old(x31);

        w3 == vars.rsa.M0D;
        w31 == 0;

        heap == old(heap);
        mem == old(mem);

        valid_wdr_view(wdrs, a_view, 4, NUM_WORDS);
        ToNat(a_view) == 0;
        mvars_inv(vars, heap, x20, x19, x16, x17, NA, NA);
{
    let _ := bn_lid_safe(x9, false, 0, x17, false, vars.m0d_it);
    bn_mov(w2, w31);

    a_view := wdrs[4..4+NUM_WORDS];

    while (Loop(x30))
        invariant
            x9 == old(x9);
            x10 + loop_ctr == 4 + NUM_WORDS;
            x11 == 2;
            x20 == old(x20);
            x21 == old(x21);
            x29 == old(x29);
            x30 == NUM_WORDS;
            x31 == old(x31);

            w2 == 0;
            w3 == vars.rsa.M0D;
            w31 == 0;

            mvars_inv(vars, heap, x20, x19, x16, x17, NA, NA);
            valid_wdr_view(wdrs, a_view, 4, NUM_WORDS);
            a_view[..x10 - 4] == SeqZero(x10 - 4);

            heap == old(heap);
            mem == old(mem);

        decreases
            loop_ctr;
    {
        let index := x10 - 4;
        a_view := a_view[index := 0];
        bn_movr(x10, true, x11, false);
    }
    assert a_view == SeqZero(NUM_WORDS);
}

procedure montmul(ghost vars: mvars)
    returns (ghost a_view: seq(uint256))

    {:frame false}

    requires
        x9 == 3;
        x10 == 4;
        x11 == 2;
    
        x30 == NUM_WORDS;
        x31 == NUM_WORDS - 1;

        mvars_inv(vars, heap, x20, x19, x16, x17, NA, NA);

        w31 == 0;
    
    ensures
        x8 == 4;
        x9 == old(x9);
        x10 == 4;
        x11 == 2;
        x21 == old(x21);
        x29 == old(x29);
        x30 == NUM_WORDS;
        x31 == NUM_WORDS - 1;

        w31 == 0;

        heap == old(heap);
        mem == old(mem);

        valid_wdr_view(wdrs, a_view, 4, NUM_WORDS);
        mvars_inv(vars, heap, NA, x19, x16, x17, NA, NA);
        montmul_inv(a_view, vars.x_it.buff, NUM_WORDS, vars.y_it.buff, vars.rsa);
{
    a_view := montmul_0(vars);

    ghost var x_it := vars.x_it;

    assert montmul_inv(a_view, x_it.buff, x_it.index, vars.y_it.buff, vars.rsa) by {
        montmul_inv_lemma_0(a_view, x_it.buff, x_it.index, vars.y_it.buff, vars.rsa);
    }

    while (Loop(x30))
        invariant
            x9 == old(x9);
            x11 == 2;
            x21 == old(x21);
            x29 == old(x29);
            x30 == NUM_WORDS;
            x31 == NUM_WORDS - 1;

            iter_inv(x_it, heap, x20);
            x_it.index + loop_ctr == NUM_WORDS;
            x_it.base_ptr == vars.x_it.base_ptr;
            x_it.buff == vars.x_it.buff;

            w3 == vars.rsa.M0D;
            w31 == 0;
    
            mvars_inv(vars, heap, NA, x19, x16, x17, NA, NA);

            montmul_inv(a_view, x_it.buff, x_it.index, vars.y_it.buff, vars.rsa);

            valid_wdr_view(wdrs, a_view, 4, NUM_WORDS);

            heap == old(heap);
            mem == old(mem);

        decreases loop_ctr;
    {
        x_it := bn_lid_safe(x11, false, 0, x20, true, x_it);
        addi(x6, x16, 0);
        addi(x7, x19, 0);

        a_view := mont_loop(x_it, w2, vars, a_view);

        addi(x16, x6, 0);
        addi(x19, x7, 0);
    }

    /* restore pointers */
    li(x8, 4);
    li(x10, 4);
}


#verbatim
}
#endverbatim
